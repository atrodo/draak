Root         => <Goal>

# Remove comments
Preprocess   => s/^[c*].*?\n//i

# Any line without line numbers need protection
Preprocess   => s/^(.{0,72})\n/$1 /i

# Continuation lines
Preprocess   => s/^(.{5}) . ([^\n]*?) (\d{8})? \n .{5} \S ([^\n]*?) (\d{8})? \n/$1 $2$4$3\n/

# Remove line numbers
Preprocess   => s/(\d{8})$//
#LineNo       => s/(\d{8})$//

Whitespace   => [[:blank:]]*


<Goal>       -> <Label> <Program> <EOL><Statements>*<Label> <End> <EOL>
<Statements> ~> m/\s*?\n/
<Statements> -> <Label> <Statement> <EOL>

<Statement>  -> <Assign>
<Statement>  -> <Assignment>
<Statement>  -> <Backspace>
<Statement>  -> <Block>
<Statement>  -> <Call>
<Statement>  -> <Character>
<Statement>  -> <Close>
<Statement>  -> <Common>
<Statement>  -> <Complex>
<Statement>  -> <Continue>
<Statement>  -> <Data>
<Statement>  -> <Dimension>
<Statement>  -> <Do>
<Statement>  -> <Double>
<Statement>  -> <EndFile>
<Statement>  -> <Entry>
<Statement>  -> <Equivalence>
<Statement>  -> <External>
<Statement>  -> <Format>
<Statement>  -> <Function>
<Statement>  -> <GoTo>
<Statement>  -> <If>
<Statement>  -> <Implicit>
<Statement>  -> <Inquire>
<Statement>  -> <Integer>
<Statement>  -> <Intrinstic>
<Statement>  -> <Logical>
<Statement>  -> <Open>
<Statement>  -> <Parameter>
<Statement>  -> <Pause>
<Statement>  -> <Print>
<Statement>  -> <Read>
<Statement>  -> <Real>
<Statement>  -> <Return>
<Statement>  -> <Rewind>
<Statement>  -> <Save>
<Statement>  -> <Stop>
<Statement>  -> <Subroutine>
<Statement>  -> <Write>

<Program>    -> PROGRAM <id>
#<Program>    ~> m/\G(\w)/
<End>        -> END

<Unit>       -> UNIT =
<Err>        -> , ERR = <id>
<Iostat>     -> , IOSTAT = <id>
<Access>     -> , ACCESS = <id>
<Blank>      -> , BLANK = <id>
<Direct      -> , DIRECT = <id>
<Exist>      -> , EXIST = <id>
<Form>       -> , FORM = <id>
<Formatted>  -> , FORMATTED = <id>
<Name>       -> , NAME = <id>
<Named>      -> , NAMED = <id>
<Nextrec>    -> , NEXTREC = <id>
<Number>     -> , NUMBER = <id>
<Opened>     -> , OPENED = <id>
<Recl>       -> , RECL = <id>
<Sequential> -> , SEQUENTIAL = <id>
<Unformatted>-> , UNFORMATTED = <id>
<Fmt>        -> , {<fmteq>} <fs>
<fmteq>      -> FMT =

<Assignment> -> <id> = <expr>

<Assign>     -> ASSIGN <id> TO <id>

<Backspace>  -> BACKSPACE <id>
<Backspace>  -> BACKSPACE ( {<Unit>} <id> {<Err>} {<Iostat>} )

<Block>      -> BLOCK DATA {<id>}

<Call>       -> CALL <id> {<actuals>}
<actuals>    -> ( <Param> <Params*> )
<Param>      -> <id>
<Params>     -> , <Param>

<Character>  -> CHARACTER {<cchars>} <id> {<clength>} {<cdim>} {<cvname>}
<cchars>     -> * <id>
<clength>    -> * <id>
<cdim>       -> ( <id> )
<cvname>     -> , <id> {<clength>} {<cdim>}

<Close>      -> CLOSE ( {<Unit>} <id> {<Err>} {<Iostat>} {<Status>} )

<Common>     -> COMMON {<Commonblock>} <nlist> <MoreCommon>*
<Commonblock>-> /<id>/
<nlist>      -> <id> <nlists>*
<nlists>     -> , <id>
<MoreCommon> -> <Commonblock> <nlist>

<Complex>    -> COMPLEX <vname> <vnames>*
<vnames>     -> , <vname>
<vname>      -> <id> {<dim>}
<dim>        -> ( <id> )

<Continue>   -> CONTINUE
#   "FORMAT" [editlist] .

<Data>       -> DATA <datalist> <datalists>*
<datalists>  -> , <datalist>
<datalist>   -> <nlist> / <clist> /
<clist>      -> <clistItem> <clistItems>*
<clistItems> -> , <clistItem>
<clistItem>  -> {<repeatable>} <constant>
<repeatable> -> <nzUnsignInt> *

<Dimension>  -> DIMENSION <dimDefine> <dimDefines>*
<dimDefines> -> , <dimDefine>
<dimDefine>  -> <id> ( <dimDecl> <dimDecls>* )
<dimDecls>   -> , <dimDecl>
<dimDecl>    -> {<dimLower>} <num>

<Do>         -> DO {<doLabel>} <id> = <num> , <num> {<doInc>}
<doInc>      -> , <num>
<doLabel>    -> <id> {<comma>}
<comma>      -> ,

<Double>     -> DOUBLE PRECISION <vname> <vnames>*

<EndFile>    -> ENDFILE <id>
<EndFile>    -> ENDFILE ( {<Unit>} <id> {<Err>} {<Iostat>} )

<Entry>      -> ENTRY <id> {<actuals>}

<Equivalence>-> EQUIVALENCE ( <nlist> ) <equivs>*
<equivs>     -> , ( <nlist> )

<External>   -> EXTERNAL <nlist> <nlists>*

<Format>     -> FORMAT <fs>
<fs>         -> ( <flist> <flists>* )
<fs>         -> <LabelRef>
<flists>     -> , <flist>
<flist>      -> {<repeatable>} <ed>
<flist>      -> {<repeatable>} <fs>
<flist>      -> <ned>

<ed>         -> I<nzUnsignInt>
<ed>         -> I<nzUnsignInt>.<unsignInt>
<ed>         -> F<nzUnsignInt>.<unsignInt>
<ed>         -> E<nzUnsignInt>.<unsignInt>
<ed>         -> E<nzUnsignInt>.<unsignInt>E<unsignInt>
<ed>         -> D<nzUnsignInt>.<unsignInt>
<ed>         -> G<nzUnsignInt>.<unsignInt>
<ed>         -> G<nzUnsignInt>.<unsignInt>E<unsignInt>
<ed>         -> L<nzUnsignInt>
<ed>         -> A
<ed>         -> A<nzUnsignInt>
#<ned>        ~> m/(?'len'\d+)H(.{\g{len}})/
<ned>        -> <chrConst>
<ned>        -> T<nzUnsignInt>
<ned>        -> TL<nzUnsignInt>
<ned>        -> TR<nzUnsignInt>
<ned>        -> <nzUnsignInt>X
<ned>        -> /
<ned>        -> :
<ned>        -> S
<ned>        -> SP
<ned>        -> SS
<ned>        -> {<integer>}P
<ned>        -> BN
<ned>        -> BZ

<Function>   -> {<type>} FUNCTION <id> ( <Param> <Params*> )

<GoTo>       -> GOTO <id>
<GoTo>       -> GO TO <LabelRef>
#<GoTo>       -> GOTO <id>
#   "GOTO" ( label | "(" labels")" [ "," ] n | variable [ [ "," ] "(" labels ")" ] ) .

<If>         -> IF ( <expr> ) <ifblock>
<ifblock>    -> <Statement>
<ifblock>    -> THEN <Statements>* <ElseIfs>* ELSE <Statements>* END IF
<ifblock>    -> <LabelRef> , <LabelRef> , <LabelRef>

<Implicit>   -> IMPLICIT <impType> <impTypes>* 
<impTypes>   -> , <impType>
<impType>    -> <type> ( <letter> <letters>* )

<letters>    -> , <letter>
<letter>     ~> m/[A-Z]/
<letter>     ~> m/[A-Z]\s*-\s*[A-Z]/

<Inquire>    -> INQUIRE ( {<Unit>} <id> <InqOptions> )
<Inquire>    -> INQUIRE ( FILE <id> <InqOptions> )
<InqOptions> -> {<Access>} {<Blank>} {<Direct>} {<Err>} {<Exist>} {<Form>} {<Formatted>} {<Iostat>} {<Name>} {<Named>} {<Nextrec>} {<Number>} {<Opened>} {<Recl>} {<Sequential>} {<Unformatted>}

<Integer>    -> INTEGER <vname> <vnames>*

<Intrinstic> -> INTRINSIC <nlist> <nlists>*

<Logical>    -> LOGICAL <vname> <vnames>*

<Open>       -> OPEN ( {<UNIT>} <id> {<Access>} {<Blank>} {<Err>} {<File>} {<Form>} {<Iostat>} {<Recl>} {<Status>}

<Parameter>  -> PARAMETER ( <nameConst> <nameConsts>* )
<nameConsts> -> , <nameConst>
<nameConst>  -> <id> = <constexpr>

<Pause>      -> PAUSE {<prompt>}
<prompt>     ~> m/(\d{1,5})/

<Print>      -> <fs> {<nlist>}

<Read>       -> READ <fs> , {<nlist>}
<Read>       -> READ {<UNIT>} <id> {<Fmt>} {<EndFile>} {<Err>} {<Iostat>} {<Recl>} {<nlist>}

<Real>       -> REAL <vname> <vnames>*

<Return>     -> RETURN {<ordinal>}

<Rewind>     -> REWIND <id>
<Rewind>     -> REWIND ( {<Unit>} <id> {<Err>} {<Iostat>} )

<Save>       -> SAVE <slist> <slists>*
<slists>     -> , <slist>
<slist>      -> <Commonblock>
<slist>      -> <id>

<Stop>       -> STOP {<message>}
<message>    ~> m/(\d{1,5})/
<message>    -> <chrConst>

<Subroutine> -> SUBROUTINE <id> <actuals>

<Write>      -> WRITE ( {<Unit>} <id> {<Fmt>} {<Err>} {<Iostat>} {<Recl>} ) {<nlist>}


<expr>       -> <iexpr> 
<expr>       -> <aexpr> 
#<expr>       -> <cexpr> 
#<expr>       -> <lexpr> 

<cexpr>      -> <cprimary>
<cexpr>      -> <cprimary> // <cexpr>

<cprimary>   -> <chrConst>
<cprimary>   -> <primary>
<cprimary>   -> <substring>
<cprimary>   -> ( <cexpr> )

<iexpr>      -> <iterm>
<iexpr>      -> + <iterm>
<iexpr>      -> - <iterm>
<iexpr>      -> <iexpr> + <iterm>
<iexpr>      -> <iepxr> - <iterm>

<iprimary>   -> <Integer>
<iprimary>   -> <iprimary>
<iprimary>   -> ( <iexpr> )

<ifactor>    -> <iprimary>
<ifactor>    -> <iprimary> ** <ifactor>

<iterm>      -> <ifactor>
<iterm>      -> <ifactor> / <iterm>
<iterm>      -> <ifactor> * <iterm>

<aexpr>      -> <aterm>
<aexpr>      -> + <aterm>
<aexpr>      -> - <aterm>
<aexpr>      -> <aterm> + <aexpr>
<aexpr>      -> <aterm> - <aexpr>

<aprimary>   -> <number>
<aprimary>   -> <primary>
<aprimary>   -> ( <aexpr> )

<afactor>    -> <aprimary>
<afactor>    -> <aprimary> ** <afactor>

<aterm>      -> <afactor>
<aterm>      -> <afactor> / <aterm>
<aterm>      -> <afactor> * <aterm>

<primary>    -> <id>
<primary>    -> <arrayelem>
<primary>    -> <id> <actuals>

<arrayelem>  -> <id> ( <subscript> <subscripts>* )
<subscripts> -> , <subscript>
<subscript>  -> <expr>

<substring>  -> <primary> ( <iexpr> : <iexpr> )

<type>       -> INTEGER
<type>       -> REAL
<type>       -> DOUBLE PRECISION
<type>       -> COMPLEX
<type>       -> LOGICAL
<type>       -> CHARACTER {<clength>}

<Label>      ~> m/([\s\d]{5})/
<LabelRef>   ~> m/([\d]{1,5})/

<EOL>        ~> m/\s*?\n/

<chrConst>   ~> m/(?:') ([^']*(?:(?:'')[^']*)*) (?:')/

<unsignInt>  ~> m/(       \d+                          )/
<integer>    ~> m/( [+-]? \d+                          )/
<real>       ~> m/( [+-]?     \. \d+ (?: E [+-]? \d+)? )/
<real>       ~> m/( [+-]? \d+ \. \d* (?: E [+-]? \d+)? )/
<real>       ~> m/( [+-]? \d+        (?: E [+-]? \d+)  )/
<double>     ~> m/( [+-]? \d+ \. \d* (?: D [+-]? \d+)  )/
<double>     ~> m/( [+-]? \d+        (?: D [+-]? \d+)  )/

<nzUnsignInt>-> <unsignInt>
<number>     -> <integer>
<number>     -> <real>
<number>     -> <double>

<complexNum> -> ( <number> , <number> )

<logical>    -> .TRUE.
<logical>    -> .False.

<id>         ~> m/([[:alpha:]]\w{0,6})/
